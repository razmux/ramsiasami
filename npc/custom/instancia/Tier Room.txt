

-	shop	nectar_shop	-1,909:100

-	script	Tier_Reward	-1,{
OnTalk:
	.@party_id = getcharid(1);
	.@map$ = strcharinfo(3);
	.@label$ = @label$;
	
	if( getcharid(0) != getpartyleader( .@party_id,2 ) ){
		message strcharinfo(0),"You're not Party Leader.";
		killmonster .@map$,"All";
		monster .@map$,50,50,"--ja--",1324,1,.@label$;
		end;
	}else{
		mes "Please ensure you distribute the Rewards. If you cancelled this, your party might not able to receive any rewards.";
		next;
		mes "Reward List :";
		for( .@i = 0; .@i < @size; .@i++ )
			mes "^777777 ~ "+getitemname( @reward[.@i] )+"^000000";
		if( prompt( "Start Distribute Rewards","Warp Out" ) == 1 ){
			getpartymember .@party_id;
			getpartymember .@party_id,1;
			getpartymember .@party_id,2;
			.@party_size = $@partymembercount;
			copyarray .@party_aid[0],$@partymemberaid[0],.@party_size;
			copyarray .@party_name$[0],$@partymembername$[0],.@party_size;
			for( .@i = 0; .@i < $@partymembercount; .@i++ ){
				if( isloggedin( $@partymemberaid[.@i],$@partymembercid[.@i] ) )
					.@party_name_menu$ = .@party_name_menu$ + $@partymembername$[.@i];
				.@party_name_menu$ = .@party_name_menu$ +":";
			}
			for( .@i = 0; .@i < @size; .@i++ ){
				next;
				mes "^777777 ~ "+getitemname( @reward[.@i] )+"^000000";
				if( prompt( "Distribute Randomly","Pick a Player" ) == 2 ){
					.@name = select( .@party_name_menu$ ) - 1;
					message .@party_name$[.@name],"Gained "+getitemname( @reward[.@i] )+" ( Distribute by Leader )";
				}else{
					.@name = rand( .@party_size );
					message .@party_name$[.@name],"Gained "+getitemname( @reward[.@i] )+" ( Distribute Randomly )";
				}
				getitem @reward[.@i],1,.@party_aid[.@name];
			}
		}
		mes "Please wait..warping out";
		mapannounce .@map$,"All Rewards distributed. Warping out in 5 seconds.",0;
		sleep2 5000;
		mapwarp .@map$,"prontera",155,181;
	}
	end;
	
}

prontera,150,166,4	script	Ancient Tapestry	757,{
.@party_id = getcharid(1);

if( !.@party_id ){
	mes "You need a Party.";
}else if( getpartyleader( .@party_id,2 ) != getcharid(0) ){
	mes "You're not Party Leader.";
}else{
	@tier = select( "Tier D","Tier C","Tier B" ) - 1;
	mes "Give me the Nectar .";
	mes "1 x "+getitemname( .nectar[@tier] );
	npcshopattach "nectar_shop",1;
	callshop "nectar_shop",2;
	end;
}
close;

OnSellItem:
	if( getmapusers( .map$ ) ){
		mes "Someone is inside the "+.npc_name$+" now. Please try again later.";
	}else if( @sold_nameid == .nectar[@tier] ){
		mes "Trade Successfully.";
		delitem @sold_nameid,1;
		killmonster .map$,"All";
		warpparty .map$,0,0,getcharid(1);
		.tier = @tier;
		monster .map$,0,0,"--ja--",getd( ".tier_"+.tier+"_mob[ "+rand( getd( ".tier_"+.tier+"_mob_size" ) )+" ]" ),1,.npc_name$+"::OnMVPKill";
	}else{
		mes "You have to give me 1 x "+getitemname( .nectar[@tier] );
	}
	close;
	
OnMVPKill:
	mapannounce .map$,"Congratz you all have finished this Tier successfully. Party Leader gained the Bio Sample and Reward Treasure Chest spawned.",0;
	getitem .bio_sample[.tier],1,getpartyleader( getcharid(1),1 );
	killmonster .map$,"All";
	monster .map$,50,50,"--ja--",1324,1,.npc_name$+"::OnBoxKill";
	end;
	
OnBoxKill:
	deletearray @reward;
	@label$ = .npc_name$+"::OnBoxKill";
	@size = getd( ".tier_reward_"+.tier+"_size" );
	copyarray @reward[0],getd( ".tier_reward_"+.tier+"[0]" ),@size;
	doevent "Tier_Reward::OnTalk";
	end;
	
OnInit:
// map used for mvp killing
.map$ = "guild_vs1-1";

// required nectar for each tier
setarray .nectar,
	32700,	// item id for clear nectar
	32701,	// item id for yellow nectar
	32702,	// item id for red nectar
	32703;	// item id for blue nectar

// tier reward
setarray .tier_reward_0,607;
setarray .tier_reward_1,608;
setarray .tier_reward_2,609;

// .tier monster
setarray .tier_0_mob,2981,2982,2983,2984,2985,2986,2987,2988;
setarray .tier_1_mob,2989,2990,2991,2992;
setarray .tier_2_mob,2993,2994;

// bio sample
setarray .bio_sample,
	32707,	// item id for bio sample
	32708,	// item id for quality bio sample
	32709;	// item id for pristine bio sample

	
.nectar_size = getarraysize( .nectar );
.tier_reward_0_size = getarraysize( .tier_reward_0 );
.tier_reward_1_size = getarraysize( .tier_reward_1 );
.tier_reward_2_size = getarraysize( .tier_reward_2 );
.tier_0_mob_size = getarraysize( .tier_0_mob );
.tier_1_mob_size = getarraysize( .tier_1_mob );
.tier_2_mob_size = getarraysize( .tier_2_mob );
.npc_name$ = strnpcinfo(0);
setmapflag .map$,mf_partylock;
setmapflag .map$,mf_nomobloot;
setmapflag .map$,mf_nomvploot;
// setmapflag .map$,mf_nocommand;
mapwarp .map$,"prontera",155,181;
end;

}




prontera,155,166,4	script	Watery Lair	757,{
.@party_id = getcharid(1);

if( !.@party_id ){
	mes "You need a Party.";
}else if( getpartyleader( .@party_id,2 ) != getcharid(0) ){
	mes "You're not Party Leader.";
}else{
	@tier = select( "Tier A" ) - 1;
	mes "Give me the Nectar .";
	mes "1 x "+getitemname( .nectar[@tier] );
	npcshopattach "nectar_shop",1;
	callshop "nectar_shop",2;
	end;
}
close;

OnSellItem:
	if( getmapusers( .map$ ) ){
		mes "Someone is inside the "+.npc_name$+" now. Please try again later.";
	}else if( @sold_nameid == .nectar[@tier] ){
		mes "Trade Successfully.";
		delitem @sold_nameid,1;
		killmonster .map$,"All";
		warpparty .map$,0,0,getcharid(1);
		.tier = @tier;
		monster .map$,0,0,"--ja--",getd( ".tier_"+.tier+"_mob[ "+rand( getd( ".tier_"+.tier+"_mob_size" ) )+" ]" ),1,.npc_name$+"::OnMVPKill";
	}else{
		mes "You have to give me 1 x "+getitemname( .nectar[@tier] );
	}
	close;
	
OnMVPKill:
	mapannounce .map$,"Congratz you all have finished this Tier successfully. Party Leader gained the Bio Sample and Reward Treasure Chest spawned.",0;
	getitem .bio_sample[.tier],1,getpartyleader( getcharid(1),1 );
	monster .map$,50,50,"--ja--",1324,1,.npc_name$+"::OnBoxKill";
	end;
	
OnBoxKill:
	deletearray @reward;
	@label$ = .npc_name$+"::OnBoxKill";
	@size = getd( ".tier_reward_"+.tier+"_size" );
	copyarray @reward[0],getd( ".tier_reward_"+.tier+"[0]" ),@size;
	doevent "Tier_Reward::OnTalk";
	end;
	
OnInit:
// map used for mvp killing
.map$ = "guild_vs1-2";

// required nectar for each tier
setarray .nectar,
	32703;  // item id for blue nectar

// tier reward
setarray .tier_reward_0,607;

// .tier monster
setarray .tier_0_mob,2995;

// bio sample
setarray .bio_sample,
	32710;	// item id for consummate bio sample
	
.nectar_size = getarraysize( .nectar );
.tier_reward_0_size = getarraysize( .tier_reward_0 );
.tier_0_mob_size = getarraysize( .tier_0_mob );
.npc_name$ = strnpcinfo(0);
setmapflag .map$,mf_partylock;
setmapflag .map$,mf_nomobloot;
setmapflag .map$,mf_nomvploot;
// setmapflag .map$,mf_nocommand;
mapwarp .map$,"prontera",155,181;
end;

}



prontera,160,166,4	script	Beacon	757,{
.@party_id = getcharid(1);

if( !.@party_id ){
	mes "You need a Party.";
}else if( getpartyleader( .@party_id,2 ) != getcharid(0) ){
	mes "You're not Party Leader.";
}else{
	@tier = select( "Tier D-2","Tier C-2","Tier B-2" ) - 1;
	mes "Give me the Ambrosia.";
	mes "1 x "+getitemname( .ambrosia[@tier] );
	npcshopattach "nectar_shop",1;
	callshop "nectar_shop",2;
	end;
}
close;

OnSellItem:
	if( getmapusers( .map$ ) ){
		mes "Someone is inside the "+.npc_name$+" now. Please try again later.";
	}else if( @sold_nameid == .ambrosia[@tier] ){
		mes "Trade Successfully.";
		delitem @sold_nameid,1;
		killmonster .map$,"All";
		warpparty .map$,0,0,getcharid(1);
		.tier = @tier;
		monster .map$,0,0,"--ja--",getd( ".tier_"+.tier+"_mob[ "+rand( getd( ".tier_"+.tier+"_mob_size" ) )+" ]" ),1,.npc_name$+"::OnMVPKill";
	}else{
		mes "You have to give me 1 x "+getitemname( .ambrosia[@tier] );
	}
	close;
	
OnMVPKill:
	mapannounce .map$,"Congratz you all have finished this Tier successfully. Bonus Tier doesnt have Bio Sample reward but Reward Treasure Chest spawned.",0;
	monster .map$,50,50,"--ja--",1324,1,.npc_name$+"::OnBoxKill";
	end;
	
OnBoxKill:
	deletearray @reward;
	@label$ = .npc_name$+"::OnBoxKill";
	@size = getd( ".tier_reward_"+.tier+"_size" );
	copyarray @reward[0],getd( ".tier_reward_"+.tier+"[0]" ),@size;
	doevent "Tier_Reward::OnTalk";
	end;
	
OnInit:
//map user for mvp killing
.map$ = "guild_vs1-3";

// ambrosia item id
setarray .ambrosia,
	32704,	// item id for clear ambrosia
	32705,	// item id for yellow ambrosia
	32706;	// item id for red ambrosia
	
// tier reward
setarray .tier_reward_0,607;
setarray .tier_reward_1,608;
setarray .tier_reward_2,609;

// .tier monster
setarray .tier_0_mob,2996,2997,2998;
setarray .tier_1_mob,2999,3000;
setarray .tier_2_mob,3001;

.nectar_size = getarraysize( .nectar );
.tier_reward_0_size = getarraysize( .tier_reward_0 );
.tier_reward_1_size = getarraysize( .tier_reward_1 );
.tier_reward_2_size = getarraysize( .tier_reward_2 );
.tier_0_mob_size = getarraysize( .tier_0_mob );
.tier_1_mob_size = getarraysize( .tier_1_mob );
.tier_2_mob_size = getarraysize( .tier_2_mob );
.npc_name$ = strnpcinfo(0);
setmapflag .map$,mf_partylock;
setmapflag .map$,mf_nomobloot;
setmapflag .map$,mf_nomvploot;
// setmapflag .map$,mf_nocommand;
mapwarp .map$,"prontera",155,181;
end;

}

